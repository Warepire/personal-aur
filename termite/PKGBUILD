# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=('termite-git-with-nN-cursor-move-patch')
pkgver=v14.r0.g1b67c5c
pkgrel=1
pkgdesc="A simple VTE-based terminal"
arch=('x86_64')
url="https://github.com/thestinger/termite/"
license=('LGPL')
depends=('vte3-ng-with-nN-cursor-move-patch' 'ncurses') # ncurses now comes with the terminfo file
makedepends=('git' 'vte3-ng-with-nN-cursor-move-patch' 'ncurses')
conflicts=(termite)
provides=(termite)
source=("git+https://github.com/thestinger/termite.git"
        "0001-Move-cursor-along-with-next-prev-search-match-select.patch"
        "0002-Change-TERM-to-termite-instead-of-xterm-termite.patch")
sha256sums=('SKIP'
            '08e20604b715f80636dd08562ceccff18cec738ea19867028757ce44cdd1b8ef'
            'b03bf663feab19b1fcfce55e8c46d778e14d49eb08dea15c5b248e3476933318')

pkgver() {
  cd termite
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd termite

  patch -p1 -i ${srcdir}/0001-Move-cursor-along-with-next-prev-search-match-select.patch
  patch -p1 -i ${srcdir}/0002-Change-TERM-to-termite-instead-of-xterm-termite.patch
}

build() {
  cd termite
  git submodule update --init
  make
}

package() {
  backup=(etc/xdg/termite/config)

  cd termite
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
