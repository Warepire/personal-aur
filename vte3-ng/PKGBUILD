# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=vte3-ng-with-nN-cursor-move-patch
pkgver=0.52.2
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
url="https://wiki.gnome.org/Apps/Terminal/VTE"
arch=(x86_64)
license=(LGPL)
options=(!emptydirs)
depends=(gtk3 pcre2 gnutls vte-common)
makedepends=(intltool gobject-introspection vala glade git gtk-doc gperf)
conflicts=(vte3)
provides=(vte3)
_commit=7322c27fee05bb6dd10289d05b79f53ac386799f  # tags/0.52.2^0
source=("git+https://git.gnome.org/browse/vte#commit=$_commit"
        "0001-expose-functions-for-pausing-unpausing-output.patch"
        "0002-expose-function-for-setting-cursor-position.patch"
        "0003-add-function-for-setting-the-text-selections.patch"
        "0004-add-functions-to-getset-block-selection-mode.patch"
        "0005-expose-function-for-getting-the-selected-text.patch"
        "0006-Add-function-to-get-selection-position.patch")
sha256sums=('SKIP'
            'e58f4fc2e6fdff80304ce5bd7b507533261d8534d004128d70bf1de4c62524d2'
            'e5fe5d281a7fbd397e279661da3b835e07215d74d4b89ea3bf028971d2dd897a'
            '22ca30a5c155b5a69bd71dc956a38aaad25e6c7e77cef5e10615e9abab6af391'
            '32c18f2e63a947b0dbf1ad029b061ec8eedec70ce162428fd0282bd035a717d8'
            'f55b57e33374b3906542ecc9b22da3e158bacb0610dcafcde916de2e118db48a'
            '6b518ff0a2ad4f58cce5fc23ee4471f563879203160aee638563832d89e8ccfe')

pkgver() {
  cd vte
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd vte

  # https://bugzilla.gnome.org/show_bug.cgi?id=778926
  git cherry-pick -n 809e79770b4dea34d64574710ce429a86855fdb2

  patch -p1 -i ${srcdir}/0001-expose-functions-for-pausing-unpausing-output.patch
  patch -p1 -i ${srcdir}/0002-expose-function-for-setting-cursor-position.patch
  patch -p1 -i ${srcdir}/0003-add-function-for-setting-the-text-selections.patch
  patch -p1 -i ${srcdir}/0004-add-functions-to-getset-block-selection-mode.patch
  patch -p1 -i ${srcdir}/0005-expose-function-for-getting-the-selected-text.patch
  patch -p1 -i ${srcdir}/0006-Add-function-to-get-selection-position.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd vte

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/vte \
    --localstatedir=/var --disable-static --enable-introspection --enable-glade-catalogue --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package(){
  cd vte
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/etc/profile.d/vte.sh
}
