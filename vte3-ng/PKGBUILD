# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=vte3-ng-with-nN-cursor-move-patch
pkgver=0.54.0
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
url="https://wiki.gnome.org/Apps/Terminal/VTE"
arch=(x86_64)
license=(LGPL)
options=(!emptydirs)
depends=(gtk3 pcre2 gnutls vte-common)
makedepends=(intltool gobject-introspection vala glade git gtk-doc gperf)
conflicts=(vte3)
provides=(vte3)
_commit=2b7729bab7ba1faa05be0c5b480c0a33f3e360aa  # tags/0.54.0
source=("git+https://git.gnome.org/browse/vte#commit=$_commit"
        "0001-expose-functions-for-pausing-unpausing-output.patch"
        "0002-expose-function-for-setting-cursor-position.patch"
        "0003-add-function-for-setting-the-text-selections.patch"
        "0004-add-functions-to-getset-block-selection-mode.patch"
        "0005-expose-function-for-getting-the-selected-text.patch"
        "0006-Add-function-to-get-selection-position.patch")
sha256sums=('SKIP'
            '31e7e79b29c0e121e61b1d175a7724764c0569b6bad41bdb84503da5506d9deb'
            '081b4365b8b10a09d22ea51b16bd1c7b11065027bbbe34d261cddf078041464a'
            'c92f34fcfb6bbf06569d7c7ef6d91d86f74750d6f6208d9273cf6a295296ad50'
            '33a788062bbaf2acd0e0d6a224b96882b818ee55cf937a3fd30787569b756aa9'
            '0b36fb13752291de9c00603a1426e08ad446fe052f04263b1216555e90f11bad'
            '99642656313e26aee209bdd3788f23607f5c7a0f992d0a55684bef20cbcfad47')

pkgver() {
  cd vte
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd vte

  # https://bugzilla.gnome.org/show_bug.cgi?id=778926
  git cherry-pick -n 809e79770b4dea34d64574710ce429a86855fdb2

  patch -p1 -i ${srcdir}/0001-expose-functions-for-pausing-unpausing-output.patch
  patch -p1 -i ${srcdir}/0002-expose-function-for-setting-cursor-position.patch
  patch -p1 -i ${srcdir}/0003-add-function-for-setting-the-text-selections.patch
  patch -p1 -i ${srcdir}/0004-add-functions-to-getset-block-selection-mode.patch
  patch -p1 -i ${srcdir}/0005-expose-function-for-getting-the-selected-text.patch
  patch -p1 -i ${srcdir}/0006-Add-function-to-get-selection-position.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd vte

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/vte \
    --localstatedir=/var --disable-static --enable-introspection --enable-glade-catalogue --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package(){
  cd vte
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/etc/profile.d/vte.sh
}
